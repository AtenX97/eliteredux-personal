raw `
Route132_MapScripts:: @ 81F74E9
	.byte 0

Route132_EventScript_Gilbert:: @ 81F74EA
	trainerbattle_single TRAINER_GILBERT, Route132_Text_GilbertIntro, Route132_Text_GilbertDefeat
	msgbox Route132_Text_GilbertPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Dana:: @ 81F7501
	trainerbattle_single TRAINER_DANA, Route132_Text_DanaIntro, Route132_Text_DanaDefeat
	msgbox Route132_Text_DanaPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Ronald:: @ 81F7518
	trainerbattle_single TRAINER_RONALD, Route132_Text_RonaldIntro, Route132_Text_RonaldDefeat
	msgbox Route132_Text_RonaldPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Kiyo:: @ 81F752F
	trainerbattle_single TRAINER_KIYO, Route132_Text_KiyoIntro, Route132_Text_KiyoDefeat
	msgbox Route132_Text_KiyoPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Paxton:: @ 81F7546
	trainerbattle_single TRAINER_PAXTON, Route132_Text_PaxtonIntro, Route132_Text_PaxtonDefeat
	msgbox Route132_Text_PaxtonPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Darcy:: @ 81F755D
	trainerbattle_single TRAINER_DARCY, Route132_Text_DarcyIntro, Route132_Text_DarcyDefeat
	msgbox Route132_Text_DarcyPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Jonathan:: @ 81F7574
	trainerbattle_single TRAINER_JONATHAN, Route132_Text_JonathanIntro, Route132_Text_JonathanDefeat
	msgbox Route132_Text_JonathanPostBattle, MSGBOX_AUTOCLOSE
	end

Route132_EventScript_Makayla:: @ 81F758B
	trainerbattle_single TRAINER_MAKAYLA, Route132_Text_MakaylaIntro, Route132_Text_MakaylaDefeat
	msgbox Route132_Text_MakaylaPostBattle, MSGBOX_AUTOCLOSE
	end

`
script Route132_EventScript_LegendaryNPC{
    lock
    faceplayer
	msgbox(format("Sage: I stand as the gatekeeper to the resting place of the Ultra Beasts. Make no mistake: despite still wearing the battle scars inflicted upon them by King Calyrex and the Tapus, they possess an immense power, so tread with caution in their presence.\p"
	"Do you want to know how they got here?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == 1){
	if (!flag(FLAG_SYS_GAME_CLEAR)){
		msgbox(format("The Ultra Beasts are creatures coming from worlds outside our galaxy. They came to Earth not by choice,\p"
		"but necessity: Necrozma's hunger didn't spare their planets, so they roamed until they found a hospitable place to live.\p"
		"Those who didn't immediately flee were either consumed or turned into soldiers for Necrozma's galactic army. Many of these\p"
		"Ultra Beasts perished in the war waged on them by the Tapus and King Calyrex, but it looks like some have survived by hiding in these small islands.\p"
		"These creatures suffered a lot in their life, I think they deserve a guardian to take care of them. Your heart seems in the right place. Once you'll be worthy of them, return to me.\p"))
	}
	else{
		if(defeated(TRAINER_MAKAYLA)  &&
		   defeated(TRAINER_JONATHAN) &&
		   defeated(TRAINER_DARCY)    &&
		   defeated(TRAINER_PAXTON)   &&
		   defeated(TRAINER_KIYO)     &&
		   defeated(TRAINER_RONALD)   &&
		   defeated(TRAINER_DANA)     &&
		   defeated(TRAINER_GILBERT)){
			msgbox(format("Your heart is in the right place, and your strength is the one of a great trainer. I added the location of some of the Ultra Beasts in your DexNav. There might be more in other routes, too: try to catch them all!"))
		}
		else{
			msgbox(format("You have demonstrated great strenght so far. But I want to see it with my eyes! Defeat these last few trainers, and I'll show you the place where the Ultra Beasts reside."))
		}
	}
	}
	else{
		msgbox(format("Stay safe out there."))
	}
    release
    end
}

script Route132_EventScript_Gyarados{
	lock
	faceplayer
    goto_if_set(FLAG_DEFEATED_ROUTE132_GYARADOS, Route132_EventScript_Gyarados_Join)
    goto_if_unset(FLAG_BADGE07_GET, Route132_EventScript_Gyarados_PreTateLiza)
    playmoncry(SPECIES_GYARADOS, 2)
    msgbox(format("Gyarados locks eyes with you, its challenging stare conveying a clear invitation to a fierce battle. Do you possess the courage to accept the challenge?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == 1){
        playmoncry(SPECIES_GYARADOS, 2)
        msgbox(format("Gyarados: Gyara!"), MSGBOX_DEFAULT)
        delay(40)
        waitmoncry
        setflag(FLAG_TOTEM_BATTLE)
        setvar(VAR_TOTEM_POKEMON_ATK_BOOST,      0)
        setvar(VAR_TOTEM_POKEMON_DEF_BOOST,      3)
        setvar(VAR_TOTEM_POKEMON_SP_ATK_BOOST,   1)
        setvar(VAR_TOTEM_POKEMON_SP_DEF_BOOST,   3)
        setvar(VAR_TOTEM_POKEMON_SPEED_BOOST,    1)
        setvar(VAR_TOTEM_POKEMON_ACCURACY_BOOST, 2)
        setvar(VAR_TOTEM_POKEMON_EVASION_BOOST,  0)
        setvar(VAR_TOTEM_MESSAGE, TOTEM_FIGHT_GYARADOS)
        setflag(FLAG_SHINY_CREATION)
        setwildbattlewithcustommoves(SPECIES_GYARADOS, 0, ITEM_GYARADOSITE_Y, MOVE_BOOMBURST, MOVE_HYDRO_PUMP, MOVE_ICE_BEAM, MOVE_HURRICANE, 0, NATURE_TIMID, 252, 252, 252, 252, 252, 252)
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        dowildbattle
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch(var(VAR_RESULT)){
            case B_OUTCOME_CAUGHT:
            case B_OUTCOME_WON:
                goto(Route132_EventScript_Gyarados_Join)
            break
            case B_OUTCOME_RAN:
            case B_OUTCOME_PLAYER_TELEPORTED:
                setvar(VAR_0x8004, SPECIES_GYARADOS)
                fadescreenswapbuffers(FADE_TO_BLACK)
                removeobject(VAR_LAST_TALKED)
                fadescreenswapbuffers(FADE_FROM_BLACK)
                bufferspeciesname(0, VAR_0x8004)
                msgbox(format("The {STR_VAR_1} ran away!"), MSGBOX_DEFAULT)
            break
        }
    }
    else{
        playmoncry(SPECIES_GYARADOS, 2)
        msgbox(format("Gyarados: Gyara..."), MSGBOX_DEFAULT)
    }
	release
	end
}

script Route132_EventScript_Gyarados_PreTateLiza{
    lock
    faceplayer
    message(format("Gyarados gazes past you, as though you're invisible, suggesting that you may want to come back when you possess the strength to rival its presence."))
    release
    end	
}

script Route132_EventScript_Gyarados_Join{
    msgbox(format("Gyarados seems to want to join your team, will you let this Gyarados join you?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == 1){
		setflag(FLAG_OBTAINED_ROUTE132_GYARADOS)
		givecustommon(SPECIES_GYARADOS, 50, ITEM_NONE, ITEM_CHERISH_BALL, NATURE_TIMID, 0, 252, 0, 0, 4, 252, 0, 0, 0, 0, 0, 0, 0, MOVE_BOOMBURST, MOVE_HYDRO_PUMP, MOVE_ICE_BEAM, MOVE_HURRICANE, TRUE)
        playfanfare(MUS_OBTAIN_ITEM)
        message(format("{PLAYER} received a Gyarados!"))
        waitfanfare
		fadescreenswapbuffers(FADE_TO_BLACK)
		removeobject(VAR_LAST_TALKED)
		fadescreenswapbuffers(FADE_FROM_BLACK)
	}
	if(!flag(FLAG_DEFEATED_ROUTE132_GYARADOS)){
    	setflag(FLAG_DEFEATED_ROUTE132_GYARADOS)
		playfanfare(MUS_OBTAIN_ITEM)
        giveitem(ITEM_GYARADOSITE_Y, 1)
        message(format("{PLAYER} received a Gyaradosite Y!"))
        waitfanfare
	}
	release
	end
}