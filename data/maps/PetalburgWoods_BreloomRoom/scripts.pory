mapscripts PetalburgWoods_BreloomRoom_MapScripts {}

script PetalburgWoods_BreloomRoom_EventScript_Breloom{
	lock
	faceplayer
    goto_if_unset(FLAG_BADGE05_GET, PetalburgWoods_BreloomRoom_EventScript_Breloom_PreNorman)
    playmoncry(SPECIES_BRELOOM, 2)
    msgbox(format("The Breloom seems to acknowledge you. It wishes to test you in battle, will you battle it?"), MSGBOX_YESNO)
	if(var(VAR_RESULT) == 1){
        playmoncry(SPECIES_BRELOOM, 2)
        msgbox(format("Breloom: Loom!"), MSGBOX_DEFAULT)
        delay(40)
        waitmoncry
        setflag(FLAG_TOTEM_BATTLE)
        setvar(VAR_TOTEM_POKEMON_ATK_BOOST,      2)
        setvar(VAR_TOTEM_POKEMON_DEF_BOOST,      2)
        setvar(VAR_TOTEM_POKEMON_SP_ATK_BOOST,   2)
        setvar(VAR_TOTEM_POKEMON_SP_DEF_BOOST,   2)
        setvar(VAR_TOTEM_POKEMON_SPEED_BOOST,    2)
        setvar(VAR_TOTEM_POKEMON_ACCURACY_BOOST, 2)
        setvar(VAR_TOTEM_POKEMON_EVASION_BOOST,  0)
        setvar(VAR_TOTEM_MESSAGE, TOTEM_FIGHT_BRELOOM)
        //setflag(FLAG_SHINY_CREATION)
        setwildbattlewithcustommoves(SPECIES_BRELOOM, 0, ITEM_BRELOOMITE, MOVE_BULLET_SEED, MOVE_MACH_PUNCH, MOVE_SPORE, MOVE_FOCUS_PUNCH, 0, NATURE_ADAMANT, 252, 252, 252, 252, 252, 252)
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        dowildbattle
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch(var(VAR_RESULT)){
            case B_OUTCOME_CAUGHT:
            case B_OUTCOME_WON:
                msgbox(format("Breloom seems to want to join your team, will you let this Breloom join you?"), MSGBOX_YESNO)
                if(var(VAR_RESULT) == 1){
                    givecustommon(SPECIES_BRELOOM, 0, ITEM_NONE, ITEM_CHERISH_BALL, NATURE_ADAMANT, 0, 252, 4, 0, 0, 252, 0, 0, 0, 0, 0, 0, 0, MOVE_BULLET_SEED, MOVE_MACH_PUNCH, MOVE_SPORE, MOVE_FOCUS_PUNCH, FALSE)
                    playfanfare(MUS_OBTAIN_ITEM)
                    message(format("{PLAYER} received a Breloom!"))
                    waitfanfare
                }
                else{
                    playmoncry(SPECIES_BRELOOM, 2)
                    msgbox(format("Breloom ran away!"), MSGBOX_DEFAULT)
                }
                setflag(FLAG_DEFEATED_PETALBURG_WOODS_BRELOOM)
                fadescreenswapbuffers(FADE_TO_BLACK)
                removeobject(VAR_LAST_TALKED)
                fadescreenswapbuffers(FADE_FROM_BLACK)

                playfanfare(MUS_OBTAIN_ITEM)
                giveitem(ITEM_BRELOOMITE, 1)
                message(format("{PLAYER} received a Breloomite!"))
                waitfanfare
            break
            case B_OUTCOME_RAN:
            case B_OUTCOME_PLAYER_TELEPORTED:
                setvar(VAR_0x8004, SPECIES_BRELOOM)
                fadescreenswapbuffers(FADE_TO_BLACK)
                removeobject(VAR_LAST_TALKED)
                fadescreenswapbuffers(FADE_FROM_BLACK)
                bufferspeciesname(0, VAR_0x8004)
                msgbox(format("The {STR_VAR_1} ran away!"), MSGBOX_DEFAULT)
            break
        }
    }
    else{
        playmoncry(SPECIES_BRELOOM, 2)
        msgbox(format("Breloom: Loom..."), MSGBOX_DEFAULT)
    }
	release
	end
}

script PetalburgWoods_BreloomRoom_EventScript_Breloom_PreNorman{
    lock
    faceplayer
    message(format("The Breloom refuses to acknowledge your presence. Perhaps it doesn't yet consider you a worthy opponent?"))
    release
    end	
}