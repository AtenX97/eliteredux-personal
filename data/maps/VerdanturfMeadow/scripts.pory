raw `
VerdanturfMeadow_MapScripts::
	.byte 0

VerdanturfMeadow_EventScript_GiftMeganiumite::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_AUDINO, VerdanturfMeadow_EventScript_TakeCareOfAudino
	msgbox VerdanturfMeadow_Text_PleaseTakePokemon, MSGBOX_DEFAULT
	setvar VAR_TEMP_1, SPECIES_AUDINO
	specialvar VAR_RESULT, GetHighestLevelInPlayerParty
	copyvar VAR_0x800A, VAR_RESULT
	givemon SPECIES_AUDINO, VAR_0x800A, ITEM_AUDINITE
	compare VAR_RESULT, 0
	goto_if_eq VerdanturfMeadow_EventScript_ReceiveAudinoParty
	compare VAR_RESULT, 1
	goto_if_eq VerdanturfMeadow_EventScript_ReceiveAudinoPC
	goto Common_EventScript_NoMoreRoomForPokemon
	end

VerdanturfMeadow_EventScript_ReceiveAudinoParty::
	call VerdanturfMeadow_EventScript_ReceivedAudinoFanfare
	msgbox gText_NicknameThisPokemon, MSGBOX_YESNO
	compare VAR_RESULT, NO
	goto_if_eq VerdanturfMeadow_EventScript_TakeCareOfAudino
	call Common_EventScript_GetGiftMonPartySlot
	call Common_EventScript_NameReceivedPartyMon
	goto VerdanturfMeadow_EventScript_TakeCareOfAudino
	end

VerdanturfMeadow_EventScript_ReceiveAudinoPC::
	call VerdanturfMeadow_EventScript_ReceivedAudinoFanfare
	msgbox gText_NicknameThisPokemon, MSGBOX_YESNO
	compare VAR_RESULT, NO
	goto_if_eq VerdanturfMeadow_EventScript_SendAudinoToPC
	call Common_EventScript_NameReceivedBoxMon
	goto VerdanturfMeadow_EventScript_SendAudinoToPC
	end

VerdanturfMeadow_EventScript_SendAudinoToPC::
	call Common_EventScript_TransferredToPC
	goto VerdanturfMeadow_EventScript_TakeCareOfAudino
	end

VerdanturfMeadow_EventScript_ReceivedAudinoFanfare::
	playfanfare MUS_OBTAIN_ITEM
	message VerdanturfMeadow_Text_PlayerReceivedAudino
	waitmessage
	waitfanfare
	bufferspeciesname 0, SPECIES_AUDINO
	return

VerdanturfMeadow_EventScript_TakeCareOfAudino::
	msgbox VerdanturfMeadow_Text_TakeCareOfAudino, MSGBOX_DEFAULT
	setflag FLAG_RECEIVED_AUDINO
	release
	end

VerdanturfMeadow_Text_PleaseTakePokemon:
	.string "Do you know the Pokémon Audino?\p"
	.string "I'm giving them away to promote\n"
	.string "Audino welfare!\p"
	.string "Some Trainers use Audino like punching\n"
	.string "bags for their Pokémon, just to make\l"
	.string "their team grow quickly.\p"
	.string "This is not OK!\p"
	.string "Please take this Audino to keep it safe\n"
	.string "from EXP-mad Trainers!$"

VerdanturfMeadow_Text_PlayerReceivedAudino:
	.string "{PLAYER} received Audino!$"

VerdanturfMeadow_Text_TakeCareOfAudino:
	.string "Take good care of Audino!$"
`

script VerdanturfMeadow_EventScript_Meganiumite_Battle {
	lock
	faceplayer
	if (flag(FLAG_BADGE05_GET)) { // Checking if the player has the badge
		//cleartrainerflag(TRAINER_KAYLEE) // Only For testing, comment this line when you are done testing
		if (!defeated(TRAINER_MEGANIUMITE_VERDANTURF_MEADOW)) {
			msgbox(format("Ah, I see you've triumphed over Norman, quite the impressive feat at your age. How about we put your skills to the test in a friendly match? If you win, I'll reward you with this Meganiumite!"), MSGBOX_YESNO) // Asking for battle
			if (var(VAR_RESULT) == 1) {
				msgbox(format("Splendid, let's start!"), MSGBOX_NPC) // When the player accepts the battle
				trainerbattle_no_intro(TRAINER_MEGANIUMITE_VERDANTURF_MEADOW, format("Your strength is truly awe-inspiring...")) // When the player defeats the trainer, shows in-battle
				msgbox(format("You truly are strong. I hope to see your growth even further into the future. But for now, take this gift from me, and use it well."), MSGBOX_NPC)
				playfanfare(MUS_OBTAIN_ITEM)
				giveitem(ITEM_MEGANIUMITE, 1) // Item given to the player
				waitmessage
			}
			else {
				msgbox(format("That's a bit disappointing. I hope you change your mind."), MSGBOX_NPC)
			}
		}
		else {
			msgbox(format("I trust you'll find delight in the token I've bestowed upon you!"), MSGBOX_NPC)
		}
	}
	else {
		msgbox(format("Greetings! Have you come to savor the fragrance of these blossoms? You strike me as someone with different interests. A Pokémon Trainer, perchance? Well, here's a proposition for you: how about dropping by after exploring Petalburg? I may just have a little something that piques your curiosity."), MSGBOX_NPC)
	}
	release
	end	
}

